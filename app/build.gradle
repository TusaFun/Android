plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version '0.9.3'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    namespace 'com.jupiter.tusa'
    compileSdk 33

    signingConfigs {
        release {
            storeFile file('key/Key')
            storePassword 'qzwsecrftb1'
            keyAlias 'key0'
            keyPassword 'qzwsecrftb1'
        }
    }

    defaultConfig {
        applicationId "com.jupiter.tusa"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            applicationVariants.all { variant ->
                variant.outputs.all {
                    if(variant.buildType.name == 'release') {
                        outputFileName = "tusa-alpha-${versionName}-SNAPSHOT.apk"
                    }
                }
            }
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "GRPC_HOST", "\"tusa.fun\""
            buildConfigField "int", "GRPC_PORT", "5020"

            resValue "string", "app_name", "Tusa (Release)"
        }

        debug {
            buildConfigField "String", "GRPC_HOST", "\"tusa.fun\""
            buildConfigField "int", "GRPC_PORT", "5020"

            resValue "string", "app_name", "Tusa (Debug)"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'io.grpc:grpc-protobuf-lite:1.56.0'
    implementation 'io.grpc:grpc-okhttp:1.56.0'
    implementation 'io.grpc:grpc-stub:1.56.0'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.22.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.56.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                    //outputSubDir = "com/jupiter/tusa"
                }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

